plugins {
    id "org.gradle.kotlin.kotlin-dsl" version "2.3.3"
    id "publishing"
    id "maven-publish"
    id "signing"
}

def kotlinVersion = libs.versions.kotlin.get()

dependencies {
    implementation(libs.kover)
    implementation(libs.dokka)
    implementation(libs.proguard.gradle)
    implementation(libs.closure.compiler)
    implementation(libs.gson)
    implementation(libs.gradle.publish.plugin)
    implementation(libs.kotlin.gradle.plugin)
    if (System.getProperty("java.version").startsWith("1.8") || System.getProperty("java.version").startsWith("9")) {
        implementation(libs.android.build.gradle.java8)
    } else {
        implementation(libs.android.build.gradle.java11)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
    maven { url = uri("https://plugins.gradle.org/m2/") }
    if (kotlinVersion.contains("eap") || kotlinVersion.contains("-")) {
        maven { url = uri("https://maven.pkg.jetbrains.space/kotlin/p/kotlin/temporary") }
        maven { url = uri("https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev") }
        maven { url = uri("https://maven.pkg.jetbrains.space/public/p/kotlinx-coroutines/maven") }
    }
    maven { url = uri("https://oss.sonatype.org/content/repositories/snapshots/") }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.suppressWarnings = true
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
java.sourceSets["main"].java {
    srcDir("src/main/kotlinGen")
}
